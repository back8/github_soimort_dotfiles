# Maintainer: Mort Yao
# Contributor: Martin Wimpress <code@flexion.org>

_pkgname=moe
pkgname=${_pkgname}
pkgver=1.16.1.r4.g7ef2776
pkgrel=2
pkgdesc="An image viewing and cataloging program for MATE (GTK2 version)"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus-glib' 'gobject-introspection-runtime' 'exempi' 'lcms2' 'libexif' 'libjpeg-turbo' 'mate-desktop=1.16.1')
makedepends=('gobject-introspection' 'intltool' 'itstool')
groups=('mate-extra')
#conflicts=("${_pkgbase}-gtk3")
source=("${_pkgname}::git://github.com/soimort/moe.git#branch=1.16")
sha1sums=('SKIP')

pkgver() {
  cd $_pkgname
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g' | sed 's/^v//'
}

build() {
  cd $_pkgname
  ./autogen.sh
  ./configure \
      --prefix=/usr \
      --localstatedir=/var \
      --with-gtk=2.0 \
      --with-librsvg \
      --disable-python

  #https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $_pkgname
  make DESTDIR="${pkgdir}" install
  mv ${pkgdir}/usr/bin/eom ${pkgdir}/usr/bin/moe
}
