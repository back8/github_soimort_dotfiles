# Maintainer: Mort Yao
# Contributor: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

_pkgname=moe
pkgname=${_pkgname}
pkgver=1.20.0.r194.gc92fb91
pkgrel=1
pkgdesc="An image viewing and cataloging program for MATE"
url="https://mate-desktop.org"
arch=('x86_64')
license=('GPL')
depends=('dbus-glib' 'gobject-introspection-runtime' 'exempi' 'lcms2' 'libexif' 'libjpeg-turbo' 'mate-desktop>=1.22' 'libpeas')
makedepends=('gobject-introspection' 'intltool' 'itstool')
groups=('mate-extra')
provides=('eom')
conflicts=('eom' 'eom-gtk3')
replaces=('eom-gtk3')
source=("${_pkgname}::git://github.com/soimort/moe.git")
sha1sums=('SKIP')

pkgver() {
  cd $_pkgname
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g' | sed 's/^v//'
}


build() {
  cd $_pkgname
  ./autogen.sh
  ./configure \
      --prefix=/usr \
      --localstatedir=/var \
      --with-librsvg

  #https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $_pkgname
  make DESTDIR="${pkgdir}" install
  mv ${pkgdir}/usr/bin/eom ${pkgdir}/usr/bin/moe
}
