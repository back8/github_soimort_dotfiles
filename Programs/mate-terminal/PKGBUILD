# Maintainer: Mort Yao
# Contributor: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-terminal
pkgver=1.23.0.r19.g53032d7
pkgrel=1
pkgdesc="The MATE Terminal Emulator"
url="https://mate-desktop.org"
arch=('x86_64')
license=('GPL')
depends=('libsm' 'mate-desktop' 'vte3')
makedepends=('intltool' 'itstool' 'python')
groups=('mate-extra')
conflicts=('mate-terminal-gtk3')
replaces=('mate-terminal-gtk3')
source=("${pkgname}::git://github.com/soimort/mate-terminal.git")
sha1sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g' | sed 's/^v//'
}

build() {
  cd $pkgname
  ./autogen.sh
  ./configure \
      --prefix=/usr \
      --sysconfdir=/etc \
      --disable-static --localstatedir=/var --with-gnu-ld

  make
}

package() {
  cd $pkgname
  make DESTDIR="${pkgdir}" install
}
